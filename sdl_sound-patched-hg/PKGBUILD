# Maintainer: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sdl_sound-patched-hg-fix
pkgver=1.0.3.r105.9262f9205898+
pkgrel=1
pkgdesc="A library to decode several popular sound file formats, patched for SDL2, pkg-config and ima-adpcm support (development version)"
arch=('i686' 'x86_64')
url="http://icculus.org/SDL_sound/"
license=('LGPL')
depends=('sdl2' 'libmodplug' 'libvorbis' 'flac' 'speex')
makedepends=('mercurial')
conflicts=("sdl_sound-patched-hg" "sdl2_sound") #"sdl_sound" 
provides=("sdl_sound-patched-hg" "sdl2_sound")
source=(${pkgname%%-*}::"hg+http://hg.icculus.org/icculus/SDL_sound"
        "${pkgname%%-*}-ima-adpcm.patch"
        "${pkgname%%-*}-change_include.patch"
        "SDL2_sound.pc")
md5sums=('SKIP'
            'bb94c13e440483f29064ad631283c754'
            'fdecbb663db7d10f2b06ef2429f8ace9'
            '4e433a5b1beab2861e15350fb1ec9b2f')

pkgver() {
  cd ${pkgname%%-*}

  _lasttag=$(hg tags -q | sort -r | grep release- | head -n1)
  _commits=$(hg log --template "{node}\n" -r $_lasttag:tip | wc -l)
  printf "%s.r%s.%s" "${_lasttag/release-}" "$_commits" "$(hg identify -i)"
}

prepare() {
  cd ${pkgname%%-*}
  patch -Np1 < ../${pkgname%%-*}-ima-adpcm.patch
  patch -Np1 < ../${pkgname%%-*}-change_include.patch
  mkdir -p "${srcdir}/build"
}

build() {
  cd "${srcdir}/build"
  cmake ../${pkgname%%-*} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSDLSOUND_BUILD_SHARED=ON \
    -DSDLSOUND_BUILD_STATIC=OFF
  make
}

package() {
  make -C "${srcdir}/build" DESTDIR="$pkgdir/" install
  rm -r "${pkgdir}/usr/bin"
  install -Dm644 "${srcdir}/SDL2_sound.pc" "${pkgdir}/usr/lib/pkgconfig/SDL2_sound.pc"
  _lasttag=$(cd "${srcdir}/${pkgname%%-*}";hg tags -q | sort -r | grep release- | head -n1)
  sed -i "s/__VERSION__/${_lasttag/release-}/g" "${pkgdir}/usr/lib/pkgconfig/SDL2_sound.pc"
}
