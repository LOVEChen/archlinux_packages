# Maintainer: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=mkxp-git
pkgver=r813.ae59fcd
pkgrel=1
pkgdesc="Open source implementation of the Ruby Game Scripting System of RPG Maker XP/VX/VX Ace (development version)"
arch=('i686' 'x86_64')
url="https://github.com/Ancurio/mkxp"
license=('GPL2')
depends=('ruby' 'sdl2' 'sdl2_image' 'sdl2_ttf' 'sdl_sound-patched-hg-fix'
         'pixman' 'openal' 'boost-libs' 'libgl' 'libsigc++' 'fluidsynth' 'physfs')
makedepends=('git' 'boost' 'cmake' 'mesa' 'vim')
conflicts=('mkxp')
provides=('mkxp')
install=mkxp.install
source=("git+$url.git" "add_cxx11_flag.patch" "fix_library_name.patch" "LiberationSans.ttf")
md5sums=('SKIP'
         '244d09e7ea9c681e884fe40bb8632724'
         '9e23aea0ac72dc352d8cb9fe9df8f499'
         '0d4e2660a06d6b185cb9a26e3c54d330')

pkgver() {
  cd mkxp
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
prepare() {
    cd mkxp
    patch -p1 < ../add_cxx11_flag.patch
    patch -p1 < ../fix_library_name.patch
    rm assets/liberation.ttf
    cp "$srcdir/LiberationSans.ttf" "$srcdir/mkxp/assets/liberation.ttf"
}

build() {
  
  cd "${srcdir}/mkxp"

  # get the major and minor version of installed ruby, remove any strings (like beta, patchlevel)
  _rubyver=$(pacman -Q ruby | sed -r 's/^ruby //;s/\.[^\.]+-.+$//;s/[^0-9\.]//')
  # check for validity, for example rubinius-ruby provides ruby, but cannot be used (no library)
  [ -z $_rubyver ] && exit 1

  cmake ./ -DMRIVERSION=$_rubyver -DWORKDIR_CURRENT=ON -DSHARED_FLUID=ON
  make
}

package() {
  cd mkxp

  # install architecture dependent executeable
  install -Dm755 mkxp.bin.x* "$pkgdir"/usr/bin/mkxp

  # install documentation
  install -Dm644 README.md "$pkgdir"/usr/share/doc/mkxp/README.md
  install -m644 mkxp.conf.sample "$pkgdir"/usr/share/doc/mkxp
}
